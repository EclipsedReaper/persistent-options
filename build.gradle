plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
    }

    loom {
        silentMojangMappingsLicense()

        mixin {
            useLegacyMixinAp = true
            defaultRefmapName = "persistent_options-refmap.json"
        }
    }

    dependencies {
        if (project.name != 'neoforge') {
            minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
            mappings loom.officialMojangMappings()
        }
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
        }
    }
}

tasks.register('deployMods', DefaultTask) {
    dependsOn ':fabric:build'
    dependsOn ':forge:build'
    dependsOn ':neoforge:build'

    doLast {
        def userHome = System.getProperty("user.home")
        def prismInstances = "${userHome}/AppData/Roaming/PrismLauncher/instances"

        println "Detected User Home: ${userHome}"
        println "Targeting Prism Instances at: ${prismInstances}"

        def deployments = [
                'fabric'  : [
                        "1.20.1 Fabric",
                        "1.20.4 Fabric",
                        "1.21.11 Fabric"
                ],
                'forge'   : [
                        "1.20.1 Forge",
                        "1.20.4 Forge"
                ],
                'neoforge': [
                        "1.20.4 NeoForge",
                        "1.21.11 NeoForge"
                ]
        ]

        deployments.each { moduleName, instanceNames ->
            def projectObj = project(":$moduleName")
            def libsDir = projectObj.layout.buildDirectory.dir("libs").get().asFile

            def jarFile = libsDir.listFiles().find { file ->
                def name = file.name
                return name.endsWith(".jar") &&
                        !name.contains("-dev") &&
                        !name.contains("-sources") &&
                        !name.contains("-javadoc")
            }

            if (jarFile != null) {
                instanceNames.each { instanceName ->
                    def destPath = file("${prismInstances}/${instanceName}/minecraft/mods")

                    if (!destPath.exists()) {
                        println "  [WARNING] Destination not found: ${destPath}"
                    } else {
                        copy {
                            from jarFile
                            into destPath
                        }
                        println "  -> Deployed to: ${instanceName}"
                    }
                }
            } else {
                println "  [ERROR] Could not find valid jar in ${moduleName}/build/libs/"
            }
        }
    }
}